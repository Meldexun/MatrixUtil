plugins {
    id 'java'
    id 'eclipse'
}

version = '1.3.0'
group = 'meldexun.matrixutil'
archivesBaseName = 'MatrixUtil'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

repositories {
    ivy {
        url 'https://github.com/'

        patternLayout {
            artifact '[organisation]/[module]/releases/download/v[revision]/[module]-[revision].[ext]'
        }

        metadataSources { artifact() }
    }
}

dependencies {
    implementation 'meldexun:MemoryUtil:1.0.0@jar'
}

jar {
    manifest.attributes([
        'Built-By': 'Meldexun',
        'Specification-Title': archivesBaseName,
        'Specification-Version': version,
        'Specification-Vendor': 'Meldexun',
        'Implementation-Title': archivesBaseName,
        'Implementation-Version': version,
        'Implementation-Vendor': 'Meldexun',
        'Implementation-Timestamp': new Date().format("yyyy-MM-dd HH:mm:ss")
    ])
}
